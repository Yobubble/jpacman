plugins {
    id 'application'
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    id 'pmd'
    id 'com.github.spotbugs' version '6.0.7'
}

sourceSets {
    defaultTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/default-test/java')
        }
        resources.srcDir file('src/default-test/resources')
    }
}

configurations {
    defaultTestImplementation.extendsFrom testImplementation
    defaultTestRuntimeOnly.extendsFrom testRuntimeOnly
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.google.guava:guava:$guavaVersion"

    compileOnly "com.github.spotbugs:spotbugs-annotations:$spotbugsAnnotationsVersion"

    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"

    // Newly Added
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.2.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.2.0'
    testImplementation group: 'org.junit.platform', name: 'junit-platform-engine', version: '1.2.0'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.11.0'
}

task defaultTest(type: Test) {
    testClassesDirs = sourceSets.defaultTest.output.classesDirs
    classpath = sourceSets.defaultTest.runtimeClasspath
}
// Don't make build depend on checkstyle due to compatibility issues
build.dependsOn -= checkstyleMain
build.dependsOn -= checkstyleTest
tasks.whenTaskAdded { task ->
    if (task.name == 'checkstyleDefaultTest') {
        build.dependsOn -= task
    }
}
test.dependsOn defaultTest

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

defaultTest {
    useJUnitPlatform()
}

jacoco {
    toolVersion = jacocoVersion
}

jacocoTestReport {
    executionData tasks.withType(Test)
    reports {
        csv.required = true
    }
}

checkstyle {
    toolVersion = '10.12.5'
    configFile file("checkstyle.xml")
    ignoreFailures = true
    maxErrors = 0
    maxWarnings = Integer.MAX_VALUE
}

// Disable checkstyle tasks due to configuration compatibility issues
tasks.withType(Checkstyle).configureEach {
    enabled = false
}

pmd {
    ruleSetFiles "pmd-rules.xml"
    ruleSets = []
    ignoreFailures = true
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    ignoreFailures = true
    reports {
        xml.required = false
        html.required = true
    }
}

task staticAnalysis(type: GradleBuild) {
    tasks = [
        'clean',
        'checkstyleMain',
        'checkstyleTest',
        'checkstyleDefaultTest',
        'pmdMain',
        'pmdTest',
        'pmdDefaultTest',
        'spotbugsMain',
        'spotbugsTest',
        'spotbugsDefaultTest',
    ]
}

mainClassName = 'nl.tudelft.jpacman.Launcher'
